<!DOCTYPE html>
<html>
<head><head>
<meta charset="UTF-8" >
<title>[glsl] murble sphere - js do it</title>
<meta name="Description" content="" >
<meta name="Keywords"  content="" >

<link rel="stylesheet" type="text/css" media="screen,print" href="style.css" >
</head>
<meta name="twitter:card" content="summary" >
<meta property="og:url" content="https://takumifukasawa.github.io/jsdoitArchives/codes/ac9ed820-36eb-509d-9a41-da7e5efe6b86/" >
<meta property="og:title" content="[glsl] murble sphere - js do it" >
<meta property="og:image" content="https:/takumifukasawa.github.io/jsdoitArchives/codes/ac9ed820-36eb-509d-9a41-da7e5efe6b86/ogp.png" >
</head>
<body>



<script type="text/v-shader" id="v-shader">    
    varying vec2 v_uv;
    void main(void) {
        v_uv = uv;
        
        gl_Position = projectionMatrix * modelViewMatrix * vec4(position, 1.);
    }
</script>

<script type="text/f-shader" id="f-shader">    
    precision mediump float;

    // 高ければ高いほど精度が高い
    #define OCTAVES 6
    
    varying vec2 v_uv;
    
    uniform vec2 u_resolution;
    uniform float u_time;
    
    float random(in vec2 st) {
        return fract(sin(dot(st.xy, vec2(12.9898, 78.233))) * 43758.5453123);
    }
    
    // Based on Morgan McGuire @morgan3d
    // https://www.shadertoy.com/view/4dS3Wd
    float noise(in vec2 st) {
        vec2 i = floor(st);
        vec2 f = fract(st);
        
        float a = random(i);
        float b = random(i + vec2(1., 0.));
        float c = random(i + vec2(0., 1.));
        float d = random(i + vec2(1., 1.));
        
        vec2 u = f * f * (3. - 2. * f);
    
        return mix(a, b, u.x) + (c - a) * u.y * (1. - u.x) + (d - b) * u.x * u.y;
    }
    
    float fbm(in vec2 st) {
        float value = 0.;
        float amplitud = .5;
        float frequency = 0.;
        
        for(int i=0; i<OCTAVES; i++) {
            value += amplitud * noise(st);
            st *= 2.;
            amplitud *= .5;
        }
        return value;
    }
    
    float pattern(in vec2 st) {
        vec2 q = vec2(
            fbm(st + vec2(0.)),
            fbm(st + vec2(5.2 + u_time / 30000., 1.3 + u_time / 10000.))
        );
        vec2 r = vec2(
            fbm(st + 4. * q + vec2(1.7 + u_time / 40000., 9.2 + u_time / 20000.)),
            fbm(st + 4. * q + vec2(8.3 + u_time / 15000., 2.8 + u_time / 25000.))
        );
        return fbm(st + 4. * r + u_time / 10000.);
    }
    
    void main(void) {
        gl_FragColor = vec4(0.);
        
        //vec2 st = gl_FragCoord.xy / u_resolution.xy;
        vec2 st = v_uv;
        st.x *= u_resolution.x / u_resolution.y;
        
        vec3 color = vec3(.6, .3, .2);
        // 高ければ高いほどちらばる
        color += pattern(st * 3.);
        
        gl_FragColor = vec4(color, 1.);
    }
</script>

<div id="wrapper"></div>

<script src="https://unpkg.com/three@0.86.0/build/three.min.js"></script>

<script src="https://unpkg.com/three@0.86.0/examples/js/controls/OrbitControls.js"></script>

<script type="text/javascript" src="index.js"></script>
</body>
</html>
